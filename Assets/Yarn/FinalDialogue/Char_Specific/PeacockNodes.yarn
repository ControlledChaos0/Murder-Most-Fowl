title: PeacockIntroduction
tags: lc:Owl rc:
---
Athena: <i>Melissa Miss Peacock. Never thought I'd get to meet her in person.</i>#s:Peacock#el:Surprised
Athena: <i>I've been listening to her music since the beginning of her career.</i>
Athena: <i>Oh my gosh and her new single, “Birds of a Feather”, is soooo good.</i>#el:Happy
Athena: Good morning Miss Peacock.#el:Neutral
Melissa: Hello darling. Quite an unfortunate morning, is it not?
Athena: Quite. I'm actually here to speak with you about this.#el:Intense
Melissa: Oh?
Athena: I'm a detective. Detective Nox. I may have a few questions for you about this.#el:Neutral
Melissa: Oh my, am I a suspect?
Athena: Not at the moment, you're not. But there's something fishy about this scene, and I'd like some help to get to the bottom of this.
Melissa: I see.
<<jump PeacockIdle>>
===

title: PeacockIdle
---
<<set $ConvoChar = "Peacock">>
<<if $Tutorial is false>>
	<<jump PeacockMain>>
<<else>>
	<<jump PeacockConversation>>
<<endif>>
===

title: PeacockConversation
tags: c:Owl:Peacock
---
Melissa: How can I help you, dear?#el:Neutral
-> Could I get your autograph?
	Melissa: Oh! Uh...
	Melissa: Perhaps now isn't the time?
-> <color=\#94e9e4>Present</color>
	<<PresentEvidence>>
-> Nothing now
	Melissa: Don't hesitate to ask.
===

title: PeacockMain
---
Melissa: How can I help you, dear?#s:Peacock#el:Neutral
-> I have a few questions for you.
	<<jump PeacockQuestions0>>
-> Could I get your autograph?
	Melissa: Oh! Uh...
	Melissa: I'm terribly sorry, but perhaps now isn't the time?
-> <color=\#94e9e4>Present</color>
	<<PresentEvidence>>
-> Nothing now
	Melissa: Don't hesitate to ask.
===

title: PeacockQuestions0
---
Melissa: Now, what would you like to ask?#el:Neutral
-> Where were you last night?
	Melissa: My, let's see. 
	Melissa: I recall drinking with Morgana around last night.
	Melissa: She was such a darling and made me my favorite cocktail.
	Athena: What did you two drink?
	Melissa: She made me a Blue Lagoon, but I don't believe she actually had any alcohol.
	Athena: ...#el:Sad
	Melissa: We chatted for a while until I went off to bed. I don't remember exactly when, but I remember I woke up in the morning around 7:55 with a nasty hangover.#el:Neutral
	<<jump PeacockQuestions1>>
-> What are your thoughts about Mallard?
	Melissa: He. Ugh. You see..
	Athena: It's ok. Take your time.
	Melissa: He and I used to get along fine, at least until I knew he was a swindler.
	Athena: Swindler?#el:Intense
	Melissa: ...#el:Neutral
	<<jump PeacockQuestions0>>
-> Do you know if anyone would want to kill him?
	Melissa: My.. well I've overheard Mr. Gander was having troubles with Mallard.
	Melissa: I don't know much, but I believe it has something to do with Gander's disdain for Mallard's business practices.
	Melissa: I hear he blames Mallard for the current state of his company...
	Melissa: I don't blame him to be honest.
	Melissa: Mallard has a way with <color=\#f3db82>spending more than he has...</color>
	Athena: What of the other passengers?
	Melissa: Well the bellboy has always given me strange vibes. There's just something.. off about him.
	Athena: That's hardly evidence.
	Melissa: I suppose not.
	Melissa: ...
	Melissa: As for Morgana, given that you're still alive, I doubt it was her.
	Athena: What do you mean?
	Melissa: *laugh* <color=\#f3db82>Well she used to be in love with that duck.</color> If she were the ex killing type, I think you'd be the first she'd kill.
	Athena: She.. what?#el:Surprised
	<<CreateClue ClueBoardCanvas CrowEx>>
	: Got Evidence <color=\#94e9e4>Morgana's Ex</color>
	Melissa: Oh dear, did you not know?
	Athena: ... #el:Sad
	Athena: Let's just move on.#el:Neutral
	Melissa: Alright..
	<<jump PeacockQuestions0>>
-> <color=\#f3db82>I heard you and Mallard had an argument.</color> <<if $PeacockArgument>>
	Melissa: Yes we had an argument. 
	Melissa: It was about the money he owed me.
	Melissa: I was upset that he somehow was unable to pay me despite that fortune he boasts about.
	Melissa: That extravagant duck was <color=\#f3db82>broke this entire time</color>...
	Melissa: I was angry in the moment, but I swear I would never actually hurt someone over this.
	Athena: People don't always kill for rational reasons.#el:Intense
	Melissa: ...
	Athena: How did you learn of lack of funds? I hear he kept up his appearances well.#el:Neutral
	Melissa: ...
	Melissa: I've said too much.
	Athena: <i>Perhaps I should ask <color=\#f3db82>someone who worked closely</color> with Mallard about this...</i>
	<<set $DuckBroke to true>>
-> <color=\#f3db82>Can you tell me anything about the other passengers?</color><<if $CockfightingArena>>
	<<jump PeacockPeople>>
-> That's all the questions
	<<jump PeacockMain>>
===

title: PeacockPeople
---
Melissa: Who do you want to hear about?
-> Morgana
    Melissa: Oh Morgana is a dear.
	Melissa: You would know.
	<<if $FixedRelationship>>
		Athena: Yeah, she's something special. 
	<<else>>
		Athena: ... #el:Sad
		Athena: Yeah she was.
	<<endif>>
	Melissa: We've been good friends lately. #el:Neutral
	Melissa: We met at that bar she used to tend. 
	Melissa: I believe it was called <i>The Painted Swan</i>.
	Athena: <i>I used to frequent that place...</i>
	Melissa: As much as she acts tough, I know the dear wouldn't hurt a fly.
	Melissa: ...
	Melissa: I must tell you, there's <color=\#f3db82>some evidence I was withholding</color>.
	Melissa: After the murder <color=\#f3db82>I entered the room</color>.
	Melissa: I was going to report the murder, but I found something incriminating.
	Melissa: I found a <color=\#f3db82>pocket knife stabbed in the bar counter</color>.
	Melissa: On the pocket knife, was a <color=\#f3db82>part of a black feather</color>.
	<<CreateClue ClueBoardCanvas BlackFeathers>>
	: Got Evidence <color=\#94e9e4>Black Feathers</color>
	Melissa: I was drunk and I feared this would be used to incriminate Morgana.
	Melissa: Foolishly, I took the feather and <color=\#f3db82>put the pocket knife in the victim's pocket</color>.
	Melissa: Please believe that Morgana wouldn't do it.
	Melissa: Anyways, she's not the only suspect <color=\#f3db82>with black feathers</color>.
    <<jump PeacockPeople>>
-> Chase
    Melissa: Chase used to frequent my arena, alongside Mallard.
	Melissa: They both enjoyed betting on the winners, though I'd say <color=\#f3db82>Mallard had an unhealthy obsession with it</color>.
	<<jump PeacockPeople>>
-> Skipper
    Melissa: ...
	Melissa: I did actually <color=\#f3db82>buy drugs from him</color>.
	Melissa: It wasn't anything too strong, just Reed.
	Melissa: But I can tell you <color=\#f3db82>he was selling much stronger stuff</color>.
	Athena: <i>That's a pretty serious accusation.</i>
	Athena: <i>I should <color=\#f3db82>interrogate Skipper about this</color>.</i>
	<<set $PenguinDrugs = true>>
    <<jump PeacockPeople>>
-> Nevermind
    <<jump PeacockQuestions0>>
===

title: PeacockQuestions1
---
Melissa: Is there anything else, dear?#el:Neutral
	-> Did you hear anything in the morning?
		Melissa: I don't recall. No.
		<<jump PeacockQuestions1>>
	-> What did you do after waking up?
		Melissa: I looked around my room for 10 minutes for antacid. When I couldn't find one, I left to ask Morgana for one.
		Melissa: To my surprise I found her in front of the parlor door. 
		Melissa: I asked about entering the parlor, but the poor thing said she lost her keys in her room. I admit I may have been a bit loud. I think I woke up Mr. Gander.
		Athena: I see. What happened after?
		Melissa: When Morgana got back, she unlocked the car door.. and..
		Melissa: That was when..
		Athena: ... you saw it.#el:Sad
		Melissa: ...
		Melissa: anyways..#el:Neutral
		<<jump PeacockQuestions1>>
	-> That's all
		<<jump PeacockQuestions0>>
===

title: PeacockDismissal
---
Melissa: I'm sorry, I'm not sure what to say.#el:Neutral
===

title: PeacockLoveLetter
---
Melissa: Oh! A letter.#el:Neutral
<<if $DeliverLetter>>
	Athena: Gander wanted me to deliver this to you.
	Melissa: Oh my. Let me see.
	Melissa: ...
	Melissa: Oh! Oh Gander!
	Athena: Was it true he was with you last night?
	Melissa: Yes, now that I think about it.
	Melissa: My memory is a little hazy, but <color=\#f3db82>he was with me for quite a while last night</color>.
	Melissa: Oh Gander... what should I say?
	Athena: I'll leave you with your thoughts.
<<else>>
	Athena: Have you seen this letter before?
	Melissa: No, it doesn't look familiar.
    
<<endif>>
===

title: PeacockBurnedNote
---
Athena: I noticed your name on this note. Do you know why Mallard would address you in a note such as this?#el:Neutral
Melissa: He did <color=\#f3db82>owe me a large sum of money</color>.
Athena: Do you know why someone may burn the note?
Melissa: ...
Athena: Do you have an idea of what the rest of the note may contain?
Melissa: He was a <color=\#f3db82>gambling addict</color>.
Athena: Gambling addict?#el:Intense
Melissa: Could I see the note, perhaps?
Athena: Sure.#el:Neutral
Melissa: ...
Melissa: My, is this a suicide note?
Athena: It would appear so.
Athena: At least <i>appears</i> so.
Athena: We've already proven this was not in fact a suicide, so <color=\#f3db82>it's very possible the murderer planted it as fake evidence</color>.
Athena: The only question is though, <color=\#f3db82>why would it be burned</color>?#el:Intense
Melissa: And how did they know about me and Mallard's-
Melissa: Nevermind.#el:Neutral
===

title: PeacockDuckReed
---
Melissa: ...
Melissa: <color=\#f3db82>Yeah that was from me</color>, but I can explain.
Melissa: I <color=\#f3db82>bought this reed on the train</color> and kept it in my bag.
Melissa: <color=\#f3db82>I went to my bag last night to grab some</color>, just because I was stressed.
Melissa: The slimy duck caught me smoking it, and threatened to report me if I didn't give him one.
Melissa: I wanted to punch the living daylights out of him, but I resisted.
Melissa: I just gave him one I already had on me and told him <color=\#f3db82>I didn't want to move that heavy briefcase again</color>.
===

title: PeacockReed
---
Athena: I found this in your safe, Melissa. #el:Intense
Melissa: I- uh.
Melissa: Please don't report this. I've just been so stressed lately.
Athena: I'm just trying to get to the bottom of the case, not bust your petty drugs.
Melissa: I swear reed is the worst I have.
<<set $PresentedReed to true>>
===

title: PeacockId
---
Melissa: Oh my, this is my Id.#el:Neutral
Athena: It was laying on your floor. You should keep it in a safe place.
Melissa: Thank you for letting me know darling.
Melissa: I wouldn't want the <color=\#f3db82>wrong person knowing my birthday</color>.
===

title: PeacockFighters
---
Athena: Do you know who the people in this photo are?#el:Neutral
Melissa: Well the Rooster is Evan Rudy. Also known in the arena as Rude Rocky.
Athena: And the other peacock?
Melissa: Oh honey. Did you not know?
Athena: Know what?
Melissa: <color=\#f3db82>That peacock was me.</color>
Athena: Wait... You're trans?#el:Surprised
Melissa: Oh hahaha. I don't even try to hide it, darling.
Melissa: I didn't even change my coloring.
Melissa: Perhaps they need to revoke your detective license.
Athena: So you were a boxer?
Melissa: Yes, back in the day. 
Melissa: I actually ran that place.
Melissa: They called me Thorn. Sharp as an arrow and impossible to get rid of, hah.
Melissa: Though I started to realize I didn't want that life.
Melissa: I found a passion for singing and discovered I wanted to be the rose, not the thorn.
Melissa: Still, I find it a little hard to escape that old arena sometimes.
Melissa: When you run a gig that so many people rely on, you can't just up and leave.
Melissa: Now I just manage it from afar.
Melissa: I know this kind of fighting is illegal, but we have a really great community.
Melissa: I would hate for anything to happen to it.
Melissa: ...
Melissa: I know you're a detective darling and you'll be going to the police with information.
Melissa: I just ask, please don't report this? 
Athena: I'll keep your secret.
Athena: <i>I'll have to decide later. I need her to trust me for now.</i>#el:Intense
<<set $CockfightingArena = true>>
Melissa: Oh thank you dear. I really appreciate it.
Melissa: I'll admit, there's <color=\#f3db82>more I wasn't telling you</color>. I'll answer any questions you have.
===

title: PeacockLovers
---
Melissa: Oh my. Is that you and Morgana? #el:Neutral
<<if $FixedRelationship>>
Athena: Yeah, it's quite an old photo. #e:Happy
Melissa: You don't seem very sad, do you?
Athena: I talked to her. We figured things out.
Melissa: Oh that's wonderful. I'm glad things are alright.
<<else>>
Athena: Yeah... #el:Sad
Athena: Do you know why she still had this?
Melissa: That would be something <color=\#f3db82>you should ask her</color>, honey.
<<endif>>

===

title: PeacockCargoManifest
---
Melissa: Yes, this looks cor-
Melissa: Wait...
Melissa: Is there no briefcase on the manifest?
Athena: It would appear he does not.
Melissa: I clearly remember moving <color=\#f3db82>a heavy briefcase off of my bag</color>.
<<CreateClue ClueBoardCanvas MissingBriefcase>>
: Got Evidence <color=\#94e9e4>Missing Briefcase</color>#el:Neutral
===